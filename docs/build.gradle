buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'org.kordamp.gradle:livereload-gradle-plugin:0.2.1'
	}
}

apply plugin: 'org.kordamp.gradle.livereload'
apply from: JAVA_GRADLE
apply plugin: 'org.asciidoctor.convert'

liveReload {
	docRoot asciidoctor.sourceDir.canonicalPath
}

repositories {
	maven { url 'http://dist.gemstone.com/maven/release' }
}

asciidoctorj {

}

tasks.findByPath("artifactoryPublish")?.enabled = false

dependencies {
	testCompile project(':spring-session'),
			project(':spring-session-data-gemfire'),
			project(':spring-session-data-mongo'),
			project(':spring-session-data-redis'),
			"org.springframework:spring-jdbc:${springVersion}",
			"org.springframework:spring-messaging:${springVersion}",
			"org.springframework:spring-webmvc:${springVersion}",
			"org.springframework:spring-websocket:${springVersion}",
			"org.springframework.security:spring-security-config:${springSecurityVersion}",
			"org.springframework.security:spring-security-web:${springSecurityVersion}",
			"org.springframework.security:spring-security-test:${springSecurityVersion}",
			"junit:junit:$junitVersion",
			"org.mockito:mockito-core:$mockitoVersion",
			"org.springframework:spring-test:$springVersion",
			"org.assertj:assertj-core:$assertjVersion",
			"com.hazelcast:hazelcast:$hazelcastVersion",
			"biz.paluch.redis:lettuce:$lettuceVersion",
			"javax.servlet:javax.servlet-api:$servletApiVersion"
}

asciidoctor {
	def ghTag = snapshotBuild ? 'master' : project.version
	def ghUrl = "https://github.com/spring-projects/spring-session/tree/$ghTag"
	attributes	'version-snapshot': snapshotBuild,
			'version-milestone': milestoneBuild,
			'version-release': releaseBuild,
			'gh-url': ghUrl,
			'gh-samples-url': "$ghUrl/samples/",
			'download-url' : "https://github.com/spring-projects/spring-session/archive/${ghTag}.zip",
			'spring-session-version' : version,
			'spring-version' : springVersion,
			'lettuce-version' : lettuceVersion,
			'hazelcast-version' : hazelcastVersion,
			'docs-itest-dir' : rootProject.projectDir.path + '/docs/src/integration-test/java/',
			'docs-test-dir' : rootProject.projectDir.path + '/docs/src/test/java/',
			'docs-test-resources-dir' : rootProject.projectDir.path + '/docs/src/test/resources/',
			'samples-dir' : rootProject.projectDir.path + '/samples/',
			'session-main-resources-dir' : rootProject.projectDir.path + '/spring-session/src/main/resources/',

			'source-highlighter' : 'coderay',
			'imagesdir':'./images',
			'icons': 'font',
			'sectanchors':'',
			'idprefix':'',
			'idseparator':'-',
			'docinfo1':'true',
			'revnumber' : project.version
}

eclipse.project.name = 'spring-session-docs'